module c_fortran_bindings

   use petscksp
   use iso_c_binding
   use pcair_data_type, only: pc_air_multigrid_data
   use pcair_shell, only: PCReset_AIR_Shell, create_pc_air_shell
   use approx_inverse_setup, only: calculate_and_build_approximate_inverse, reset_inverse_mat
   use gmres_poly_apply, only: shell_poly_block_apply
   use cf_splitting, only: compute_cf_splitting
   use matdiagdomsubmatrix, only: compute_diag_dom_submatrix
   use petsc_helper, only: remove_from_sparse_match
   use air_data_type_routines, only: create_air_data
   use fc_smooth_block, only: ensure_air_block_temps

#include "petsc/finclude/petscksp.h"

   implicit none

   public

   ! -------------------------------------------------------------------------------------------------------------------------------
   ! -------------------------------------------------------------------------------------------------------------------------------
   ! Iso C bindings for PFLARE routines  
   ! -------------------------------------------------------------------------------------------------------------------------------
   ! -------------------------------------------------------------------------------------------------------------------------------      

   contains 

   !------------------------------------------------------------------------------------------------------------------------

   subroutine create_pc_air_data_c(pc_air_data_c_ptr) bind(C,name='create_pc_air_data_c')

      ! Creates an air_data object, calls setup and returns a C pointer

      ! ~~~~~~~~
      type(c_ptr), intent(inout)             :: pc_air_data_c_ptr

      type(pc_air_multigrid_data), pointer   :: pc_air_data
      ! ~~~~~~~~     

      allocate(pc_air_data)
      call create_air_data(pc_air_data%air_data)
      ! Pass the setup pc_air_data object back into C 
      pc_air_data_c_ptr = c_loc(pc_air_data)

   end subroutine create_pc_air_data_c 

   !------------------------------------------------------------------------------------------------------------------------

   subroutine PCReset_AIR_Shell_c(pc_ptr) bind(C,name='PCReset_AIR_Shell_c')

      ! Calls the Fortran routine

      ! ~~~~~~~~
      integer(c_long_long), intent(inout) :: pc_ptr

      type(tPC)  :: pc
      PetscErrorCode :: ierr
      ! ~~~~~~~~

      pc%v = pc_ptr

      ! Call the destroy routine
      call PCReset_AIR_Shell(pc, ierr)

   end subroutine PCReset_AIR_Shell_c  
   
   !------------------------------------------------------------------------------------------------------------------------

   subroutine create_pc_air_shell_c(pc_air_data_c_ptr, pc_ptr) bind(C,name='create_pc_air_shell_c')

      ! Calls the setup routine for air and returns a PC Shell as a long long
      ! The longlong pointer is defined in PC%v to pass in

      ! ~~~~~~~~
      type(c_ptr), intent(inout)          :: pc_air_data_c_ptr
      integer(c_long_long), intent(inout) :: pc_ptr

      type(pc_air_multigrid_data), pointer   :: pc_air_data
      type(tPC)  :: pc
      ! ~~~~~~~~

      ! Should have already been allocated in setup_pc_air_data_c
      call c_f_pointer(pc_air_data_c_ptr, pc_air_data)
      ! Now the input mat long long just gets copied into pc%v
      ! This works as the PETSc types are essentially just wrapped around
      ! pointers stored in %v
      pc%v = pc_ptr

      ! Call the setup routine
      call create_pc_air_shell(pc_air_data, pc)

      ! Now the PC has been modified so make sure to copy the pointer back
      pc_ptr = pc%v

   end subroutine create_pc_air_shell_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine pcair_shell_get_pcmg_c(pc_ptr, pcmg_ptr) bind(C,name='pcair_shell_get_pcmg_c')

      ! Returns the PCMG underneath our PCShell as a long long

      ! ~~~~~~~~
      integer(c_long_long), intent(in)  :: pc_ptr
      integer(c_long_long), intent(out) :: pcmg_ptr

      type(tPC)   :: pc
      type(pc_air_multigrid_data), pointer :: pc_air_data => null()
      PetscErrorCode :: ierr
      ! ~~~~~~~~

      pc%v = pc_ptr
      call PCShellGetContext(pc, pc_air_data, ierr)
      pcmg_ptr = pc_air_data%pcmg%v

   end subroutine pcair_shell_get_pcmg_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine pcair_shell_block_matapply_c(pc_ptr, x_ptr, y_ptr, applied_int, error_int) &
         bind(C,name='pcair_shell_block_matapply_c')

      ! Applies the air multigrid to a whole dense block of right hand sides
      ! The pc handed in is our underlying PCShell (which must already have been
      ! set up, as this doesn't go through PCApply on the shell)
      ! applied_int comes back as 0 if we couldn't do a block apply, in which case
      ! the caller has to apply column by column instead
      ! error_int carries back any petsc error so the caller can PetscCall it

      ! ~~~~~~~~
      integer(c_long_long), intent(in) :: pc_ptr, x_ptr, y_ptr
      integer(c_int), intent(out)      :: applied_int, error_int

      type(tPC)   :: pc, pcmg_pc
      type(tMat)  :: x_mat, y_mat
      type(pc_air_multigrid_data), pointer :: pc_air_data => null()
      PetscErrorCode :: ierr
      ! ~~~~~~~~

      pc%v    = pc_ptr
      x_mat%v = x_ptr
      y_mat%v = y_ptr

      applied_int = 0
      error_int = 0
      call PCShellGetContext(pc, pc_air_data, ierr)
      if (ierr /= 0) then
         error_int = int(ierr, c_int)
         return
      end if

      ! Defensive - the hierarchy hasn't been built so we have nothing to apply
      if (pc_air_data%air_data%no_levels == -1) return
      pcmg_pc = pc_air_data%pcmg
      if (PetscObjectIsNull(pcmg_pc)) return

      ! Build the dense scratch the block smooths need - we only know the number
      ! of columns (and the type of block) once we get here
      call ensure_air_block_temps(pc_air_data%air_data, x_mat, ierr)
      if (ierr /= 0) then
         error_int = int(ierr, c_int)
         return
      end if

      ! PCMG (or the single level PCMAT/PCJACOBI) does the rest
      call PCMatApply(pc_air_data%pcmg, x_mat, y_mat, ierr)
      error_int = int(ierr, c_int)

      applied_int = 1

   end subroutine pcair_shell_block_matapply_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine calculate_and_build_approximate_inverse_c(input_mat_ptr, inverse_type, &
         poly_order, poly_sparsity_order, &
         matrix_free_int, diag_scale_polys_int, subcomm_int, &
         coeffs_ptr, row_size, col_size, &
         inv_matrix_ptr) &
         bind(C, name='calculate_and_build_approximate_inverse_c')

      ! Builds an approximate inverse, with optional coefficient passing.
      !
      ! coeffs_ptr/row_size/col_size are in/out:
      !   On entry, if coeffs_ptr is c_null_ptr: compute fresh polynomial coefficients.
      !     On return, coeffs_ptr points to a C-malloc'd copy of the coefficients;
      !     the caller owns this memory and must free it with C free().
      !   On entry, if coeffs_ptr is non-null: reuse those coefficients; the polynomial
      !     computation is skipped (see calculate_and_build_approximate_inverse).
      !     coeffs_ptr/row_size/col_size are unchanged on return.


      ! Interface to C stdlib malloc
      interface
         function c_malloc(sz) bind(C, name='malloc')
            use iso_c_binding
            integer(c_size_t), value :: sz
            type(c_ptr) :: c_malloc
         end function c_malloc
      end interface

      ! ~~~~~~~~
      integer(c_long_long), intent(in)                   :: input_mat_ptr
      integer(c_int), value, intent(in)                  :: inverse_type, poly_order, poly_sparsity_order
      integer(c_int), value, intent(in)                  :: matrix_free_int, diag_scale_polys_int, subcomm_int
      type(c_ptr), intent(inout)                         :: coeffs_ptr
      PetscInt, intent(inout)      :: row_size, col_size
      integer(c_long_long), intent(inout)                :: inv_matrix_ptr

      type(tMat)  :: input_mat, inv_matrix
      logical     :: matrix_free, subcomm, diag_scale_polys
      PetscReal, dimension(:, :), contiguous, pointer :: coefficients
      type(c_ptr) :: c_buf
      PetscReal, pointer :: c_view(:,:)
      PetscInt :: nr, nc
      ! ~~~~~~~~

      input_mat%v = input_mat_ptr
      ! inv_matrix_ptr could be passed in as null or as an existing matrix
      ! whose sparsity we want to reuse, so we have to pass that in too
      inv_matrix%v = inv_matrix_ptr

      matrix_free    = .FALSE.
      diag_scale_polys = .FALSE.
      subcomm        = .FALSE.
      if (matrix_free_int    == 1) matrix_free    = .TRUE.
      if (diag_scale_polys_int == 1) diag_scale_polys = .TRUE.
      if (subcomm_int        == 1) subcomm        = .TRUE.

      if (c_associated(coeffs_ptr)) then

         ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         ! Reuse path: wrap the caller's buffer in a Fortran pointer and pass it in.
         ! calculate_and_build_approximate_inverse uses the null-mat trick to skip
         ! polynomial computation when coefficients is already associated on entry.
         ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call c_f_pointer(coeffs_ptr, coefficients, [int(row_size), int(col_size)])

      else

         ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         ! Fresh path: nullify so calculate_and_build_approximate_inverse allocates
         ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         nullify(coefficients)

      end if

      call calculate_and_build_approximate_inverse(input_mat, inverse_type, &
               poly_order, poly_sparsity_order, &
               matrix_free, diag_scale_polys, subcomm, &
               inv_matrix, coefficients)

      if (.NOT. c_associated(coeffs_ptr)) then
         ! Fresh path: Fortran allocate may use a compiler-specific allocator
         ! (e.g. _mm_malloc on Intel) that is incompatible with C free().
         ! Copy the data into a C-malloc'd buffer so the C side can safely free() it.
         nr = int(size(coefficients, 1), PETSC_INT_KIND)
         nc = int(size(coefficients, 2), PETSC_INT_KIND)
         c_buf = c_malloc(int(nr, c_size_t) * int(nc, c_size_t) * int(PETSC_REAL_KIND, c_size_t))
         call c_f_pointer(c_buf, c_view, [int(nr), int(nc)])
         c_view = coefficients
         ! For non-matrix-free: the matshell does not exist, so the Fortran allocation
         ! is no longer needed once we have the C copy.
         ! For matrix-free: the matshell owns its Fortran allocation (own_coefficients=.TRUE.)
         ! and will deallocate it independently via reset_inverse_mat. The C copy is
         ! stored separately in poly_coeffs and freed via free() in PCReset_PFLAREINV_c.
         if (.NOT. matrix_free) deallocate(coefficients)
         coeffs_ptr = c_buf
         row_size   = nr
         col_size   = nc
      end if

      ! Pass out the new inverse matrix
      inv_matrix_ptr = inv_matrix%v

   end subroutine calculate_and_build_approximate_inverse_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine reset_inverse_mat_c(mat_ptr) bind(C,name='reset_inverse_mat_c')

      ! Calls the Fortran routine

      ! ~~~~~~~~
      integer(c_long_long), intent(inout) :: mat_ptr

      type(tMat)  :: mat
      ! ~~~~~~~~

      mat%v = mat_ptr
      call reset_inverse_mat(mat)
      mat_ptr = mat%v

   end subroutine reset_inverse_mat_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine pflareinv_shell_block_matapply_c(shell_ptr, x_ptr, y_ptr, applied_int) &
         bind(C,name='pflareinv_shell_block_matapply_c')

      ! Applies one of the matrix-free polynomial matshells to a block of
      ! right hand sides
      ! applied_int comes back as 0 if we couldn't do a block apply, in which case
      ! the caller has to apply column by column instead - that includes being handed
      ! a matshell whose context isn't one of the polynomials shell_poly_block_apply knows

      ! ~~~~~~~~
      integer(c_long_long), intent(in) :: shell_ptr, x_ptr, y_ptr
      integer(c_int), intent(out)      :: applied_int

      type(tMat)  :: shell_mat, x_mat, y_mat
      logical     :: block_applied
      ! ~~~~~~~~

      shell_mat%v = shell_ptr
      x_mat%v     = x_ptr
      y_mat%v     = y_ptr

      call shell_poly_block_apply(shell_mat, x_mat, y_mat, block_applied)

      applied_int = 0
      if (block_applied) applied_int = 1

   end subroutine pflareinv_shell_block_matapply_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine compute_cf_splitting_c(input_mat_ptr, skip_symmetrize_int, &
         strong_threshold, max_luby_steps, &
         cf_splitting_type, ddc_its, fraction_swap, &
         is_fine_ptr, is_coarse_ptr) &
         bind(C,name='compute_cf_splitting_c')

      ! Computes a CF splitting
      ! skip_symmetrize_int skips symmetrizing the strength matrix

      ! ~~~~~~~~
      integer(c_long_long), intent(in)       :: input_mat_ptr
      integer(c_int), value, intent(in)      :: skip_symmetrize_int, max_luby_steps, cf_splitting_type, ddc_its
      PetscReal, value, intent(in)           :: strong_threshold, fraction_swap
      integer(c_long_long), intent(inout)    :: is_fine_ptr, is_coarse_ptr

      type(tMat)  :: input_mat
      type(tIS)   :: is_fine, is_coarse
      logical     :: skip_symmetrize
      ! ~~~~~~~~

      ! Now the input mat long long just gets copied into input_mat%v
      ! This works as the PETSc types are essentially just wrapped around
      ! pointers stored in %v
      input_mat%v = input_mat_ptr

      skip_symmetrize = skip_symmetrize_int == 1
      call compute_cf_splitting(input_mat, skip_symmetrize, &
                        strong_threshold, max_luby_steps, &
                        cf_splitting_type, ddc_its, fraction_swap, &
                        is_fine, is_coarse)

      ! Pass out the IS's
      is_fine_ptr = is_fine%v
      is_coarse_ptr = is_coarse%v

   end subroutine compute_cf_splitting_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine compute_diag_dom_submatrix_c(input_mat_ptr, max_dd_ratio, output_mat_ptr) &
         bind(C,name='compute_diag_dom_submatrix_c')

      ! Computes a diagonally dominant submatrix

      ! ~~~~~~~~
      integer(c_long_long), intent(in)       :: input_mat_ptr
      PetscReal, value, intent(in)           :: max_dd_ratio
      integer(c_long_long), intent(inout)    :: output_mat_ptr

      type(tMat)  :: input_mat, output_mat
      ! ~~~~~~~~

      ! Copy the input matrix pointer into the Fortran PETSc handle wrapper
      input_mat%v = input_mat_ptr

      call compute_diag_dom_submatrix(input_mat, max_dd_ratio, output_mat)

      ! Pass out the resulting submatrix handle
      output_mat_ptr = output_mat%v

   end subroutine compute_diag_dom_submatrix_c

   !------------------------------------------------------------------------------------------------------------------------

   subroutine remove_from_sparse_match_c(input_mat_ptr, output_mat_ptr, lump_int, alpha_int, alpha) &
         bind(C, name='remove_from_sparse_match_c')

      ! Restrict input_mat onto output_mat's sparsity pattern. The underlying
      ! Fortran remove_from_sparse_match auto-dispatches between the CPU and
      ! Kokkos implementations based on the matrix type, so this C entry point
      ! works for both paths.

      ! ~~~~~~~~
      integer(c_long_long), intent(in)    :: input_mat_ptr
      integer(c_long_long), intent(inout) :: output_mat_ptr
      integer(c_int), value, intent(in)   :: lump_int
      integer(c_int), value, intent(in)   :: alpha_int
      ! PetscReal by public-API contract (pflare.h / PCAIR.c already commit to it)
      PetscReal, value, intent(in)         :: alpha

      type(tMat) :: input_mat, output_mat
      logical    :: lump
      ! ~~~~~~~~

      input_mat%v  = input_mat_ptr
      output_mat%v = output_mat_ptr
      lump         = (lump_int /= 0)

      if (alpha_int /= 0) then
         call remove_from_sparse_match(input_mat, output_mat, lump, alpha)
      else
         call remove_from_sparse_match(input_mat, output_mat, lump)
      end if

      ! The matrix's identity isn't replaced by remove_from_sparse_match (only
      ! its values), but pass the handle back through anyway to match the
      ! pattern used by the other C wrappers in this module.
      output_mat_ptr = output_mat%v

   end subroutine remove_from_sparse_match_c

   !------------------------------------------------------------------------------------------------------------------------

end module c_fortran_bindings

