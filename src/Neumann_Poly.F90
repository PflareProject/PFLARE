module neumann_poly

   use petscmat
   use gmres_poly, only: build_gmres_polynomial_inverse
   use gmres_poly_apply, only: petsc_matvec_right_scale_poly_mf, &
         petsc_matvec_ida_neumann_poly_mf, petsc_matvec_poly_transpose_mf
   use matshell_data_type, only: mat_ctxtype
   use tsqr, only: tsqr_buffers
   use pflare_parameters, only: PFLAREINV_NEUMANN, MF_VEC_DIAG, MF_VEC_RHS, MF_VEC_TEMP, &
         PFLARE_ONE, PFLARE_MINUS_ONE

#include "petsc/finclude/petscmat.h"

   implicit none
   public
   
   contains

! -------------------------------------------------------------------------------------------------------------------------------

   subroutine calculate_and_build_neumann_polynomial_inverse(matrix, poly_order, &
                  buffers, coefficients, poly_sparsity_order, matrix_free, &
                  reuse_mat, reuse_submatrices, inv_matrix)


      ! Builds an assembled neumann polynomial approximate inverse
      ! If poly_sparsity_order < poly_order it will build a fixed sparsity approximation
      ! Scales by the diagonal and applies the scaled diagonal to the rhs of the output
      ! so it can be used
      ! The coefficients passed in must all be one (the caller sets this) - the matshell
      ! points at this storage in the matrix-free case, exactly like the gmres builders,
      ! so the ownership rules in calculate_and_build_approximate_inverse hold for all
      ! the polynomial types

      ! ~~~~~~
      type(tMat), target, intent(in)      :: matrix
      integer, intent(in)                 :: poly_order
      type(tsqr_buffers), intent(inout)   :: buffers
      PetscReal, dimension(:), target, intent(inout) :: coefficients
      integer, intent(in)                 :: poly_sparsity_order
      logical, intent(in)                 :: matrix_free
      type(tMat), intent(inout)           :: reuse_mat, inv_matrix
      type(tMat), dimension(:), pointer, intent(inout)   :: reuse_submatrices

      ! Local variables
      integer :: comm_size, errorcode
      PetscErrorCode :: ierr
      MPIU_Comm :: MPI_COMM_MATRIX
      VecType :: vtype
      PetscInt :: local_rows, local_cols, global_rows, global_cols
      type(tMat) :: temp_mat
      type(tVec) :: rhs_copy, diag_inverse_vec
      type(mat_ctxtype), pointer :: mat_ctx=>null(), mat_ctx_scaled=>null()

      ! ~~~~~~    

      ! Have to allocate heap memory if matrix-free as the context in inv_matrix
      ! just points at the coefficients
      if (matrix_free) then

         ! We might want to call the gmres poly creation on a sub communicator
         ! so let's get the comm attached to the matrix and make sure to use that 
         call PetscObjectGetComm(matrix, MPI_COMM_MATRIX, ierr)    
         ! Get the comm size 
         call MPI_Comm_size(MPI_COMM_MATRIX, comm_size, errorcode)        
   
         ! Get the local sizes
         call MatGetLocalSize(matrix, local_rows, local_cols, ierr)
         call MatGetSize(matrix, global_rows, global_cols, ierr)      
         
         ! If not re-using
         if (PetscObjectIsNull(inv_matrix)) then

            ! Have to dynamically allocate this
            allocate(mat_ctx)
            ! The inner matshell we build below applies I - D^-1 A rather than D^-1 A
            ! This is what tells the block (multiple rhs) apply which arithmetic to use
            mat_ctx%neumann_inner = .TRUE.

            ! Create the matshell
            call MatCreateShell(MPI_COMM_MATRIX, local_rows, local_cols, global_rows, global_cols, &
                        mat_ctx, inv_matrix, ierr)
            ! The subroutine petsc_matvec_right_scale_poly_mf applies
            ! q(mat) D^-1
            call MatShellSetOperation(inv_matrix, &
                        MATOP_MULT, petsc_matvec_right_scale_poly_mf, ierr)
            ! The subroutine petsc_matvec_poly_transpose_mf applies the transpose of
            ! the above - it builds a transposed twin of this matshell on demand,
            ! see ensure_transpose_mat
            call MatShellSetOperation(inv_matrix, &
                        MATOP_MULT_TRANSPOSE, petsc_matvec_poly_transpose_mf, ierr)

            call MatAssemblyBegin(inv_matrix, MAT_FINAL_ASSEMBLY, ierr)
            call MatAssemblyEnd(inv_matrix, MAT_FINAL_ASSEMBLY, ierr)
            ! Have to make sure to set the type of vectors the shell creates
            call MatGetVecType(matrix, vtype, ierr)
            call MatShellSetVecType(inv_matrix, vtype, ierr)
            
            ! Create temporary vector we use during horner
            ! Make sure to use matrix here to get the right type (as the shell doesn't know about gpus)            
            call MatCreateVecs(matrix, mat_ctx%mf_temp_vec(MF_VEC_TEMP), PETSC_NULL_VEC, ierr) 

            ! ~~~~~~~~~~~~~
            ! Now we allocate a new matshell that applies a diagonally scaled version of 
            ! the matrix minus I
            ! ~~~~~~~~~~~~~

            ! Have to dynamically allocate this
            allocate(mat_ctx_scaled)

            ! Create the matshell
            call MatCreateShell(MPI_COMM_MATRIX, local_rows, local_cols, global_rows, global_cols, &
                        mat_ctx_scaled, mat_ctx%mat_scaled, ierr)
            ! The subroutine petsc_matvec_ida_neumann_poly_mf applies I - D^-1 A
            call MatShellSetOperation(mat_ctx%mat_scaled, &
                        MATOP_MULT, petsc_matvec_ida_neumann_poly_mf, ierr)

            call MatAssemblyBegin(mat_ctx%mat_scaled, MAT_FINAL_ASSEMBLY, ierr)
            call MatAssemblyEnd(mat_ctx%mat_scaled, MAT_FINAL_ASSEMBLY, ierr)   
            ! Have to make sure to set the type of vectors the shell creates
            call MatGetVecType(matrix, vtype, ierr)
            call MatShellSetVecType(mat_ctx%mat_scaled, vtype, ierr)
            
            ! Create temporary vector we use during horner
            ! Make sure to use matrix here to get the right type (as the shell doesn't know about gpus)            
            call MatCreateVecs(matrix, mat_ctx%mf_temp_vec(MF_VEC_RHS), mat_ctx%mf_temp_vec(MF_VEC_DIAG), ierr)       

         ! Reusing 
         else
            call MatShellGetContext(inv_matrix, mat_ctx, ierr)
            call MatShellGetContext(mat_ctx%mat_scaled, mat_ctx_scaled, ierr)

         end if

         ! Free old owned coefficients before reassigning, exactly as the gmres
         ! builders do - avoids a memory leak when the same matshell is reused across
         ! PCSetUp calls (SAME_NONZERO_PATTERN) but fresh coefficients are passed in
         if (mat_ctx%own_coefficients .AND. associated(mat_ctx%coefficients)) then
            deallocate(mat_ctx%coefficients)
            mat_ctx%coefficients => null()
         end if

         ! The matshell points at the caller's coefficient storage (all ones)
         ! Ownership is the caller's decision, exactly as with the gmres builders:
         ! calculate_and_build_approximate_inverse sets own_coefficients after we
         ! return, and for PCAIR it stays .FALSE. as PCAIR manages its own storage
         mat_ctx%coefficients => coefficients
         mat_ctx_scaled%coefficients => coefficients

         ! This is the matrix whose inverse we are applying (just copying the pointer here)
         mat_ctx%mat = matrix
         mat_ctx_scaled%mat = matrix

         ! Get the diagonal
         call MatGetDiagonal(matrix, mat_ctx%mf_temp_vec(MF_VEC_DIAG), ierr)
         mat_ctx_scaled%mf_temp_vec(MF_VEC_DIAG) = mat_ctx%mf_temp_vec(MF_VEC_DIAG)

      ! If not matrix free
      else

         ! The coefficients we've been passed are already all one

         ! Need to build an assembled I - D^-1 A
         call MatDuplicate(matrix, MAT_COPY_VALUES, temp_mat, ierr)
         call MatCreateVecs(matrix, rhs_copy, diag_inverse_vec, ierr)
         call MatGetDiagonal(matrix, diag_inverse_vec, ierr)
         call VecReciprocal(diag_inverse_vec, ierr)
         call MatDiagonalScale(temp_mat, diag_inverse_vec, PETSC_NULL_VEC, ierr) 
   
         ! Computes: I - D^-1 A
         call MatScale(temp_mat, PFLARE_MINUS_ONE, ierr)
         call MatShift(temp_mat, PFLARE_ONE, ierr)
         
         ! If we feed in coefficients=1 and leave buffers%R_buffer_receive unallocated as it will just skip 
         ! the "gmres" coefficient calculation and just calculate the sum of (potentailly fixed sparsity) matrix powers
         ! We set diag_scale_polys to false as we have already scaled the matrix by the diagonal
         ! and we scale the columns ourself next
         call build_gmres_polynomial_inverse(temp_mat, poly_order, buffers, coefficients, &
               poly_sparsity_order, .FALSE., .FALSE., reuse_mat, reuse_submatrices, inv_matrix)
               
         ! Now this computes (I - D^-1 A)^-1 D^-1
         ! For the F-point smoothing and grid-transfer operators in our air multigrid, 
         ! this is equivalent to using (I - Dff^-1 Aff)^-1 Dff^-1 everywhere we normally use 
         ! Aff^-1
         call MatDiagonalScale(inv_matrix, PETSC_NULL_VEC, diag_inverse_vec, ierr) 
         
         ! Cleanup
         call VecDestroy(rhs_copy, ierr)
         call VecDestroy(diag_inverse_vec, ierr)  
         call MatDestroy(temp_mat, ierr)

      end if      

   end subroutine calculate_and_build_neumann_polynomial_inverse


! -------------------------------------------------------------------------------------------------------------------------------

end module neumann_poly

